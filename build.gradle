apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'codenarc'

// see https://github.com/jenkinsci/job-dsl-plugin/wiki/User-Power-Moves#using-libraries
defaultTasks 'libs'

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
        }
    }
    test {
        groovy {
            srcDirs 'src/test/groovy'
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://repo.jenkins-ci.org/releases/' }
}

configurations {
    libs
}

dependencies {
    
    compile(':shared')

    // for AnalyticsTasks
    libs 'org.yaml:snakeyaml:1.17'
    compile 'org.yaml:snakeyaml:1.17'

    compile 'org.codehaus.groovy:groovy:2.1.3'
    compile 'org.jenkins-ci.plugins:job-dsl-core:1.42'
    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude module: 'groovy-all'
    }

    // for the RestApiScriptRunner
    compile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.2') {
        exclude(module: 'groovy')
    }

    // for the RestApiScriptRunner and XmlOutputRunner
    compile('ant:ant:1.7.0')
}

task cleanLibs(type: Delete) {
    delete 'lib'
}

task libs(type: Copy) {
    into 'lib'
    from configurations.libs
}

tasks.getByName('test').inputs.files(sourceSets.jobs.groovy.srcDirs)

task rest(dependsOn: 'classes', type: JavaExec) {
    main = 'com.dslexample.rest.RestApiScriptRunner'
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
}

task debugXml(dependsOn: 'classes', type: JavaExec) {
    main = 'com.dslexample.debug.XmlOutputRunner'
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
}

codenarc {
    toolVersion = '0.25.2'
    configFile = file('config/codenarc/codenarcRules.groovy')
    maxPriority2Violations = 49
    maxPriority3Violations = 91
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
